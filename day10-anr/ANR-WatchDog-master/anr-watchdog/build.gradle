
apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

group = "com.github.anrwatchdog"
version = "1.4.0"

sourceCompatibility = 1.6

task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

configurations {
    provided
    compile.extendsFrom provided
}

android {
    compileSdkVersion 29
    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 28
    }
}

dependencies {
    implementation 'androidx.annotation:annotation:1.1.0'
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'ANR-Watchdog'
                packaging 'jar'
                artifactId 'anrwatchdog'
                description 'A simple watchdog that detects Android ANR (Application Not Responding) error and throws a meaningful exception'
                url 'https://github.com/SalomonBrys/ANR-WatchDog'

                licenses {
                    license {
                        name 'MIT'
                        url 'http://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id 'salomonbrys'
                        name 'Salomon BRYS'
                        email 'salomon.brys@gmail.com'
                    }
                }

                scm {
                    url 'https://github.com/SalomonBrys/ANR-WatchDog'
                    connection 'scm:git:https://github.com/SalomonBrys/ANR-WatchDog.git'
                    developerConnection 'scm:git:git@github.com:SalomonBrys/ANR-WatchDog.git'
                    tag 'HEAD'
                }

                issueManagement {
                    system 'GitHub Issues'
                    url 'https://github.com/SalomonBrys/ANR-WatchDog/issues'
                }
            }
        }
    }
}
